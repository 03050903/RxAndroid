buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.netflix.nebula:gradle-rxjava-project-plugin:2.+'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.+'
  }
}

apply plugin: 'rxjava-project'
bintrayUpload.enabled = false

def libraryModules = subprojects.findAll { it.name != 'sample-app' }
configure(libraryModules) {
  apply plugin: 'rxjava-project'
  apply plugin: 'java'
  apply plugin: 'provided-base'

  dependencies {
    compile "io.reactivex:rxjava:$rxJavaVersion"
    provided 'com.google.android:android:4.0.1.2'
    provided 'com.google.android:support-v4:r7'

    // testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('org.robolectric:robolectric:2.4') {
      exclude group: 'com.android.support'
    }
  }
}

